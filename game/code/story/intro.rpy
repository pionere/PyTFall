init:
    image logo = ProportionalScale("content/events/Intro/logo-transperent.webp", 600, 300)
    image he = ProportionalScale("content/events/Intro/h1.webp", 1750, 550)
    image hes = ProportionalScale("content/events/Intro/he1.webp", 1750, 550)
    $ flash = Fade(.75, 0.25, .75, color="darkred")
    $ sflash = Fade(.25, 0, .25, color="darkred")

label intro:
    stop world
    stop music
    scene black
    if persistent.intro:
        menu:
            "Skip Intro?"

            "Yes":
                return
            "No":
                $ pass


    show expression Text("Mundiga continent", style="TisaOTM", align=(0.5, 0.33), size=40) as txt1:
        alpha 0
        linear 3.5 alpha 1.0
    show expression Text("3596 AD", style="TisaOTM", align=(0.5, 0.66), size=35) as txt2:
        alpha 0
        1
        linear 3.5 alpha 1.0
    pause 4
    hide txt1
    hide txt2
    with dissolve

    play world "intro-1.mp3" fadein 2.0 fadeout 2.0
    "Through trade and seemingly endless supply of slaves from the City of Crossgate, which grew from a small provincial city into the Slave Trade capital of the world in a matter of years, many new city-states arose."

    show bg story dark_city with dissolve
    "One of them is PyTFall, a relatively small town in neutral lands on the border of the Median Empire."

    show bg jail with dissolve
    "It was a time when slaves had fewer rights than pets..."
    extend " They were severely punished and could be executed on the spot by their masters without repercussions for the slightest infraction."

    show bg story slq with dissolve
    "Cruelty and lust for power of the Masters knew no bounds, and they carelessly kept breeding and buying more and more slaves..."
    extend " and soon found themselves outnumbered ten to one."

    "They say there were those who sympathized with the oppressed. Those who gave them weapons and magic to fight back. "
    show bg story firetome with ImageDissolve("content/gfx/masks/m21.webp", 2)
    extend "To resist!"

    show bg story war with dissolve
    "Riots happened one after the other. It is easy to take up arms when you have nothing to lose."
    "First few incidents were violently suppressed by the Masters..."
    extend " but it was too late."
    "The conflict quickly turned into a full-scale warfare."

    show bg story p2 with dissolve:
        subpixel True
        size (config.screen_width, config.screen_height)
        crop (0, 0, config.screen_width, config.screen_height)
        linear 30 crop (config.screen_width/2, config.screen_height/2, config.screen_width/8, 100)
    show he with dissolve:
        yalign 1.0 xpos 400
    "Amid the chaos that took hold of the city, a stranger appeared, claiming to be a historian."
    "He told the Masters of an underground sanctuary not too far from the city, where an ancient Star slept."
    show bg story ruin1 with dissolve:
        subpixel True
        size (config.screen_width, config.screen_height)
        crop (0, 0, config.screen_width, config.screen_height)
        linear 30 crop (config.screen_width/2, config.screen_height/2, config.screen_width/8, 100)
    show hes with dissolve:
        xpos 400 yalign 1.0
    "A dreadful weapon, capable of unimaginable destruction"
    show bg story ruin2 with dissolve:
        subpixel True
        size (config.screen_width, config.screen_height)
        crop (0, 0, config.screen_width, config.screen_height)
        linear 30 crop (config.screen_width/2, config.screen_height/2, config.screen_width/32, 100)
    extend " and of crushing the rebels once and for all..."
    "Carrying enough power to build a new empire and maybe, even to surpass Crossgate's wealth and fame..."

    show bg story ruin2:
        linear 3 alpha 0
    show hes:
        linear 3 alpha 0
    $ renpy.pause (3.0, hard=True)
    play world "tremor.mp3" fadein 2.0 fadeout 2.0
    scene black with dissolve
    show bg story dark_city with dissolve
    pause 1.0
    show layer master at damage_shake(0.05, (-10, 10))
    pause 2.0
    show layer master
    $ renpy.pause (1.0, hard=True)
    show bg story sky1 with wipedown:
        alpha 0
        linear 2.0 alpha 1.0
    $ renpy.pause (1.0, hard=True)
    play music "content/sfx/music/intro-2.mp3" fadeout 2.0
    show bg story sky2 with flash
    show layer master at damage_shake(0.05, (-10, 10))
    pause 2.0
    show layer master
    show bg story sky2 with sflash
    pause 1.0
    show bg story sky2 with sflash
    pause 1.0
    show bg story sky3 with flash
    pause 1.0
    show bg story sky3 with sflash
    pause 1.0
    show bg story ash2 with wipeup:
        alpha 0
        linear 2.0 alpha 1.0
    pause 2.0
    show bg story ash2:
        subpixel True
        size (config.screen_width, config.screen_height)
        crop (0, 0, config.screen_width, config.screen_height)
        linear 10 crop (config.screen_width/2, config.screen_height/2, config.screen_width/16, config.screen_height/16)
    pause 5.0
    scene bg story ash1 with dissolve:
        subpixel True
        size (config.screen_width, config.screen_height)
        crop (0, 0, config.screen_width, config.screen_height)
        linear 10 crop (config.screen_width/2, config.screen_height/2, config.screen_width/16, config.screen_height/16)
    pause 0.5
    $ renpy.show("logo", at_list=[simple_zoom_from_to_with_linear(0.5, 2, 8), Transform(pos=(0.5, 0.75), subpixel=True)])
    $ renpy.with_statement(dissolve)
    pause 4.5
    stop music fadeout 1.0
    scene black
    with dissolve
    pause 1.0
    return
